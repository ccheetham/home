#!/usr/bin/env zsh

basedir=$(dirname $0)/../..

source $basedir/etc/profile
source $ME_LIBEXEC_DIR/sh_functions

OPENJDK_REPO=https://github.com/adoptopenjdk

set -e

main () {
  hdr refreshing jdk
  local destdir=$ME_PLATFORM_TOOL_DIR
  install_version $destdir $ME_JDK8_VERSION
  install_version $destdir $ME_JDK11_VERSION
  install_version $destdir $ME_JDKLATEST_VERSION
  link_jdk $ME_JDK11_VERSION "$destdir/jdk"
  link_jdk $ME_JDKLATEST_VERSION "$destdir/jdk-latest"
}

install_version () {
  local destdir=$1
  local major=$(echo $2 | cut -d: -f1)
  local version=$(echo $2 | cut -d: -f2)
  local build=$(echo $2 | cut -d: -f3)
  local platform=
  case $ME_PLATFORM in
    linux-x64)
      platform=linux
      ;;
    osx-x64)
      platform=mac
      ;;
    *)
      die "unsupported platform: $ME_PLATFORM"
      ;;
  esac
  msg "installing jdk $version"
  local distfile=
  local disturl=
  case $major in
    8)
      distfile=openjdk${major}u-jdk_x64_${platform}_hotspot_$(echo $version | tr -d '-').tar.gz
      disturl=${OPENJDK_REPO}/openjdk${major}-binaries/releases/download/jdk${version}/${distfile}
      ;;
    10)
      distfile=openjdk${major}_x64_${platform}_jdk-${version}.tar.gz
      disturl=${OPENJDK_REPO}/openjdk${major}-releases/releases/download/jdk-${version}/${distfile}
      ;;
    *)
      distfile=openjdk${major}u-jdk_x64_${platform}_hotspot_$(echo $version | tr '+' '_').tar.gz
      disturl=${OPENJDK_REPO}/openjdk${major}-binaries/releases/download/jdk-${version}/${distfile}
      ;;
  esac
  msg "fetching $disturl"
  wget --continue -P $ME_DISTFILE_DIR $disturl
  msg "extracting $distfile"
  mkdir -p $destdir
  rm -rf $destdir/jdk$version
  tar xzf $ME_DISTFILE_DIR/$distfile -C $destdir
  # create convenience link
  local tgt=${destdir}/jdk-${major}
  local src=jdk
  [[ $major != 8 ]] && src+='-'
  src+=${version}
  [[ $ME_PLATFORM == osx-x64 ]] && src+='/Contents/Home'
  link "$src" "$tgt"
}

link_jdk () {
  local src=jdk-$(echo $1 | cut -d: -f1)
  local tgt=$2
  link "$src" "$tgt"
}

main $@
